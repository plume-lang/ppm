require "add"
require "../config"
require "../../library/file"
require "../pretty"
require "std:io/system"
require "types"
require "std:io/file"

fn build_packages(ppm_path: str): unit {
  pkgs = get_all_packages(file_config)

  pkgs.map(fn (pkg) {
    url = "https://github.com/$pkg"
    pkg_path = "$ppm_path/modules/$pkg"

    if does_directory_exist(pkg_path) {
      info("Already installed $pkg")
      return unit
    } else { 
      res = execute_command("git clone $url $pkg_path")

      if res != 0 {
        error("Failed to clone $url")
      } else {
        info("Installed $pkg")
      }
    }
  })
}

fn build_cmd(_: str, args: list<Argument>): unit {
  config_main = get_string(file_config, "main")
  interpreter_config = get_string(file_config, "interpreter")

  interpreter = (interpreter_config or args.get_option("e")).or_else("native")
    
  return switch (config_main, get_env("PPM_PATH")) {
    case (Some(filename), Some(path)) {
      if does_file_exist("$filename.plm") {
        build_packages(path)
        res = spawn_process("plumec", ["$filename.plm", "-e", interpreter])
        println(res.snd())
        return unit
      } else {
        error("File $filename.plm not found")
        return unit
      }
    }

    case (None, ?) => 
      error("No ppm.config file found")
    
    case (?, None) =>
      error("PPM_PATH not set")
  }
}
