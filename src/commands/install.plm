fn create_global_script(file: str) => "#!/bin/sh
node $file \"$@\""

fn install_cmd(_, args: list<Argument>): async<unit> {
  project_name = args[0]
    .map(fn(arg) => switch arg {
      case PositionalArgument(value) => value
      case ? => "."
    })
    .or_else(".")

  ppm_path = get_env("PPM_PATH")

  switch ppm_path {
    case Some(ppm) {
      config_file_path = "$cwd/$project_name/ppm.config"
      config = await read_config(config_file_path)
      main_entry = config >>= fn(c) => c.get_rule("main")
      project = (config >>= fn(c) => c.get_rule("name"))

      project_ = switch project {
        case Some(String(value)) => value
        case ? => project_name
      }

      switch main_entry {
        case Some(String(value)) {
          info("Building $project_")

          execute_command("plumec $cwd/$project_name/$value.plm")
          global_script = create_global_script("$cwd/$project_name/$value.js")
          ppm_writefile("$ppm/bin/$project_", global_script)
          execute_command("chmod +x $ppm/bin/$project_")

          success("Installed $project_ to $ppm/bin/$project_")

          return unit.async()
        }

        case ? {
          error("No main entry found in $config_file_path")
          return unit.async()
        }
      }
    }

    case None {
      error("PPM_PATH environment variable not set")
      
      return unit.async()
    }
  }
}